group = "com.github.kratorius.jelf"

def javaVersion = JavaVersion.VERSION_1_7

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceSets {
    perf
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:2.0.3'
    testCompile 'junit:junit:4.11'

    perfCompile project
    perfCompile 'org.openjdk.jmh:jmh-core:0.9.5'
    perfCompile 'org.openjdk.jmh:jmh-generator-annprocess:0.9.5'
}

idea {
    module {
        scopes.PROVIDED.plus  += configurations.perfCompile
        scopes.PROVIDED.minus += configurations.compile
    }
}

eclipse {
    classpath {
        plusConfigurations += configurations.perfCompile
    }
}

task perf(type: JavaExec, dependsOn: [classes, perfClasses]) {
    description 'Execute the benchmark timing of this project'
    maxHeapSize '2g'
    main 'org.openjdk.jmh.Main'
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    classpath = sourceSets.perf.runtimeClasspath
}
